{-# LANGUAGE TypeFamilies      #-}
{-# LANGUAGE FlexibleContexts  #-}
{-# LANGUAGE DefaultSignatures #-}
{-# LANGUAGE CPP               #-}
module Raaz.Core.Random
  ( PRG(..), Random(..), Seedable(..), fillRandom
  , SystemPRG
  , newSystemPRG
  ) where

import Control.Applicative
import Control.Monad   (void)
import Data.Word
import Foreign.Ptr     (castPtr)
import Foreign.Storable(Storable, peek)

import Prelude

import Raaz.Core.Types
import Raaz.Core.Random.PRG
import Raaz.Core.Random.SystemPRG

-- | Stuff that can be generated by a pseudo-random generator.
class Random r where
  getRandom :: PRG prg => prg -> IO r

  default getRandom :: (PRG prg, Storable r) => prg -> IO r
  getRandom = go undefined
    where go       :: (PRG prg, Storable a) => a -> prg -> IO a
          go w prg = let sz = byteSize w in
            allocaBuffer sz $ \ ptr -> do
              void $ fillRandomBytes sz ptr prg
              peek $ castPtr ptr

instance Random Word
instance Random Word16
instance Random Word32
instance Random Word64

instance Random w => Random (LE w) where
  getRandom = fmap littleEndian . getRandom

instance Random w => Random (BE w) where
  getRandom= fmap bigEndian . getRandom

instance (Random a, Random b) => Random (a,b) where
  getRandom prg = (,) <$> getRandom prg <*> getRandom prg

instance (Random a, Random b, Random c) => Random (a,b,c) where
  getRandom prg = (,,) <$> getRandom prg <*> getRandom prg <*> getRandom prg
